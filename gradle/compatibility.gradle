configurations {
    binaryCompatibility
}

dependencies {
    def excludeList = [
        'ktor-client-tests',
        'ktor-client-js',
        'ktor-client-json-tests',
        'ktor-client-ios',
        'ktor-client',
        'ktor-client-features'
    ].toSet()

    def projects = [].toSet()
    def queue = [
        project(":ktor-client"),
        project(":ktor-http"),
        project(":ktor-network"),
        project(":ktor-utils")
    ]

    while (!queue.isEmpty()) {
        def project = queue.remove(queue.size() - 1) as Project
        if (projects.add(project)) {
            queue += project.childProjects.values()
        }
    }

    binaryCompatibility projects.findAll { !excludeList.contains(it.name) }
}

def buildList = {
    def allModules = configurations.binaryCompatibility.allDependencies.findAll { it instanceof ProjectDependency }

    return allModules
        .sort { it.name }
        .findAll {
        def isCommon = it.dependencyProject.file("common").exists()
        def isJvm = it.dependencyProject.file("jvm").exists()
        isCommon || isJvm ? it.dependencyProject : null
    }
}

project.ext.buildList = buildList
